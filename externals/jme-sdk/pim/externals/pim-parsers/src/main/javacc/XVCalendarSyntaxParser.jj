/*
 * Copyright (c) 2004 Harrie Hazewinkel. All rights reserved.
 */

/*
 * Funambol is a mobile platform developed by Funambol, Inc.
 * Copyright (C) 2003 - 2007 Funambol, Inc.
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of the GNU Affero General Public License version 3 as published by
 * the Free Software Foundation with the addition of the following permission
 * added to Section 15 as permitted in Section 7(a): FOR ANY PART OF THE COVERED
 * WORK IN WHICH THE COPYRIGHT IS OWNED BY FUNAMBOL, FUNAMBOL DISCLAIMS THE
 * WARRANTY OF NON INFRINGEMENT  OF THIRD PARTY RIGHTS.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 * details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program; if not, see http://www.gnu.org/licenses or write to
 * the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston,
 * MA 02110-1301 USA.
 *
 * You can contact Funambol, Inc. headquarters at 643 Bair Island Road, Suite
 * 305, Redwood City, CA 94063, USA, or at email address info@funambol.com.
 *
 * The interactive user interfaces in modified source and object code versions
 * of this program must display Appropriate Legal Notices, as required under
 * Section 5 of the GNU Affero General Public License version 3.
 *
 * In accordance with Section 7(b) of the GNU Affero General Public License
 * version 3, these Appropriate Legal Notices must retain the display of the
 * "Powered by Funambol" logo. If the display of the logo is not reasonably
 * feasible for technical reasons, the Appropriate Legal Notices must display
 * the words "Powered by Funambol".
 */

options {
    STATIC = false;
    UNICODE_INPUT = true;
}

PARSER_BEGIN(XVCalendarSyntaxParser)

package com.funambol.common.pim.xvcalendar;

import java.io.InputStreamReader;
import java.util.*;

import com.funambol.common.pim.*;

public class XVCalendarSyntaxParser {

    private XVCalendarSyntaxParserListener listener;

    public void parse() throws ParseException {
        listener.start();
        try {
            XVCalendar();
        } finally {
            listener.end();
        }
    }

    public void setListener(XVCalendarSyntaxParserListener listener) {
        this.listener = listener;
    }


}

PARSER_END(XVCalendarSyntaxParser)

void XVCalendar() : {
}
{
    <BEGIN_VCAL_T>
    <CRLF_T>
    CalItems()
    <END_VCAL_T>
    (
        <CRLF_T>
    )*
    <EOF>
}

void CalItems() : {
    ParserProperty property;
}
{
    (
        VEventItem()
    |
        VToDoItem()
    |
        property=Property()
        { listener.addProperty(property); }
    )*
}

void VEventItem() : {
    ParserProperty property;
}
{
    <BEGIN_VEVENT_T>
    { listener.startEvent(); }
    <CRLF_T>
    (
        property=Property()
        { listener.addEventProperty(property); }
    )+
    <END_VEVENT_T>
    { listener.endEvent(); }
    <CRLF_T>
}

void VToDoItem() : {
    ParserProperty property;
}
{
    <BEGIN_VTODO_T>
    { listener.startToDo(); }
    <CRLF_T>
    (
        property=Property()
        { listener.addToDoProperty(property); }
    )+
    <END_VTODO_T>
    { listener.endToDo(); }
    <CRLF_T>
}

ParserProperty Property() : {
    Token name;
    ArrayList parameters = new ArrayList();
    StringBuffer value = new StringBuffer();
}
{
    name=<IDENTIFIER_T>
    Parameters(parameters)
    (<COLON_T> | <COLON_T_QP>)
    PropertyValue(value)
    <CRLF_T>
    { return new ParserProperty(name.toString(),
                                false,
                                parameters,
                                value.toString());
    }
}

void PropertyValue(StringBuffer buffer) : {
    Token value;
}
{
    (
        value=<PROP_LINEFOLD_T>
        // do nothing
    |
        value=<PROP_VALUE_T>
        { buffer.append(value.toString()); }
    |
    value=<PROP_LINEFOLD_T_QP>
    // do nothing
    |
    value=<PROP_VALUE_T_QP>
        { buffer.append(value.toString()); }
    |
        value=<PROP_EXCEPTIONS_T_QP>
    { buffer.append(value.toString()); }
    |
        value=<PROP_EXCEPTIONS_SPLIT_T_1_QP>
        {
            String qpSplit = value.toString();
            qpSplit = qpSplit.substring(0,2) + qpSplit.charAt(qpSplit.length() - 1);
            buffer.append(qpSplit);
        }
    |
        value=<PROP_EXCEPTIONS_SPLIT_T_2_QP>
        {
            qpSplit = value.toString();
            qpSplit = '=' + qpSplit.substring(qpSplit.length() - 2);
            buffer.append(qpSplit);
        }
    )*
}

void Parameters(ArrayList parameters) : {
}
{
    (
         Parameter(parameters)
    )*
}

void Parameter(ArrayList parameters) : {
    
    StringBuffer nameBuf = new StringBuffer();
    StringBuffer valueBuf = new StringBuffer();
    
    String nameString = null;
    String valueString = null;
}
{
// Here we combine the parameters from differen states.
// The lexical state seperates them. Other option is duplicate
// the grammar rule.
    ((
        <SEMICOLON_T>
        ParameterNameValue(nameBuf)
        {
            nameString = nameBuf.toString();
        }
    (
        <EQUALS_T>
         ParameterNameValue(valueBuf)
        { 
            valueString = valueBuf.toString(); 
        }
    )?)
    |(
        <QP_PARAMETER_T>
        { 
            nameString = "ENCODING";
            valueString = "QUOTED-PRINTABLE";
        }
    ))
    { 
        parameters.add(new ParserParam(nameString,
                valueString)); 
    }

}


void ParameterNameValue(StringBuffer buffer) : {
    Token value;
}
{
    (
        value=<PARAM_LINEFOLD_T>
        // do nothing
    |
        value=<PARAMETER_T>
        { buffer.append(value.toString()); }
    )*
}


/**
 * Tokens used for parsing.
 */
TOKEN : {
    <BEGIN_VCAL_T: "BEGIN:VCALENDAR">
}
TOKEN : {
    <BEGIN_VEVENT_T: "BEGIN:VEVENT">
}
TOKEN : {
    <BEGIN_VTODO_T: "BEGIN:VTODO">
}
TOKEN : {
    <END_VCAL_T: "END:VCALENDAR">
}
TOKEN : {
    <END_VEVENT_T: "END:VEVENT">
}
TOKEN : {
    <END_VTODO_T: "END:VTODO">
}
TOKEN : {
    <WS_T: (" "|"\t")>
}
<*>
TOKEN : {
    <CRLF_T: ("\r\n" | "\n")+ > : DEFAULT // State DEFAULT, if not already
}

<PARAMLIST_STATE, PARAMLIST_WITH_QP_STATE>
TOKEN : {
    <SEMICOLON_T: ";"> // stay in same state
|   <EQUALS_T: "="> // stay in same state
|   <PARAMETER_T: (~["\r", "\n", ";", ":", "="])+> // stay in same state - Can also be a parameter value
|   <QP_PARAMETER_T: ";ENCODING=QUOTED-PRINTABLE" | ";QUOTED-PRINTABLE"> : PARAMLIST_WITH_QP_STATE
|   <PARAM_LINEFOLD_T: (<CRLF_T> <WS_T>) > // stay in same state
}

TOKEN : {
    <IDENTIFIER_T: ["a"-"z", "A"-"Z"] (("-" ["a"-"z", "A"-"Z", "0"-"9"]) | ["a"-"z", "A"-"Z", "0"-"9"])*>
        : PARAMLIST_STATE
}

<PARAMLIST_STATE>
TOKEN : {
    <COLON_T: ":"> : PROP_VALUE_STATE
}

<PARAMLIST_WITH_QP_STATE>
TOKEN : {
    <COLON_T_QP: ":"> : PROP_VALUE_STATE_QP
}

<PROP_VALUE_STATE>
TOKEN : { 
          <PROP_VALUE_T: (~["\r","\n"])+ > // stay in same state
|         <PROP_LINEFOLD_T: (("\r")? "\n" [" ", "\t"]) > // stay in same state
}
<PROP_VALUE_STATE_QP>
TOKEN : {
          <PROP_VALUE_T_QP: (~["\r","\n","="])+ > // stay in same state
|         <PROP_EXCEPTIONS_T_QP: "=" ["0"-"9", "A"-"F", "a"-"f"] ["0"-"9", "A"-"F", "a"-"f"]>
              // stay in same state
|         <PROP_LINEFOLD_T_QP: ("=\r\n" | "=\n") > // stay in same state
|         <PROP_EXCEPTIONS_SPLIT_T_1_QP: "=" ["0"-"9", "A"-"F", "a"-"f"] <PROP_LINEFOLD_T_QP> ["0"-"9", "A"-"F", "a"-"f"]>
              // stay in same state
|         <PROP_EXCEPTIONS_SPLIT_T_2_QP: "=" <PROP_LINEFOLD_T_QP> ["0"-"9", "A"-"F", "a"-"f"] ["0"-"9", "A"-"F", "a"-"f"]>
              // stay in same state          
}

